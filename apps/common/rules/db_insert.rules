# Copyright (c) 2013-2014 Silke Horn, Andreas Paffenholz
# http://solros.de/polymake/poly_db
# http://www.mathematik.tu-darmstadt.de/~paffenholz
# 
# This file is part of the polymake extension polyDB.
# 
# polyDB is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# polyDB is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with polyDB.  If not, see <http://www.gnu.org/licenses/>.


sub adjust_properties_at_level {
	my ($prefix, $input_object, $output_object, $properties, $options) = @_;

	my %knownproperties;
	if ( $prefix eq "" ) {
		%knownproperties = map { $_->property->name => 1 } @{$input_object->contents};	
	} else {
		%knownproperties = map { $_->property->name => 1 } @{$input_object->give($prefix)->contents};	
	}
	foreach my $pv (keys %$properties) {		
		my $prop = $pv;
		if ( $prefix ne "" ) {
			$prop = $prefix.".".$pv;
		}
		if ( $properties->{$pv} == 1 ) {
			if ( exists($knownproperties{$pv} ) ) {
				if ( !$options->{keep_all_props} ) { # otherwise it is in $output_object already
					# we get property from $input_object
					my $val = $input_object->give($prop);
					# and pass them on to $output_object
					$output_object->take($prop,$val);
					
				}
			} else { 
				if ( !$options->{nonew} ) {
					# FIXME using a schedule apparently does not work
					# my $s = $input_object->get_schedule($pv);
					# $s->apply($output_object);
				
					# we compute properties in $input_object
					my $val = $input_object->give($prop);
					# and pass them on to $output_object
					$output_object->take($prop,$val);
				}
			}
		} else {
			if ( exists($knownproperties{$pv}) || !$options->{nonew} ) {
				adjust_properties_at_level($prop, $input_object, $output_object, $properties->{$pv}, $options);
			}
		}
	}	
	
}

sub adjust_properties {
	my ($input_object, $properties, $type, $options ) = @_;
	
	my $output_object;
	if ( $options->{keep_all_props} ) {
		$output_object = $input_object->copy;
		$output_object->begin_transaction;
	} else {
	# create a new polytope
		$output_object=eval("new ".$type.";");
		$output_object->name        = $input_object->name;
		$output_object->credits     = $input_object->credits;
		$output_object->description = $input_object->description;
	}

	adjust_properties_at_level("",$input_object,$output_object,$properties,$options);
	
	$output_object->commit;
	return $output_object;
}

sub insert_into_col {
	my ($obj, $collection, $id, $options) = @_;	
	
	my $use_template = 0;
	my $temp;
	if ( defined($temp = $options->{template}) ) {
		$use_template = 1;
	}
	
	# prepare metadata
	my $metadata = {};
	
	# set the contributor, precedence is
	# given in object > set as option > given in template
	if ( defined($options->{"contributor"}) ) {
		$metadata->{"contributor"} = $options->{"contributor"};
	} else {
		if ( defined($temp->{"contributor"}) ) {
			$metadata->{"contributor"} = $temp->{"contributor"};
		}
	}

	if ( defined($options->{template_key}) ) {
		$metadata->{"template_key"} = $options->{template_key};
	}

	# check if polytope has a polyDB attachment
	if ( defined( my $polydb_attachment = $obj->get_attachment("polyDB") ) ) {
		foreach (@{$polydb_attachment}) {
			$metadata->{$_} = $polydb_attachment->{$_};
		}
	} 
	
	my $json;	
	if ( $use_template ) {
		# returns a new polymake object with the properties of $obj copied to it if all should be kept or in template, properties required by template computed and nonoew not set
		$obj = adjust_properties($obj, $temp->{"xml_template"}, $temp->{"basic_type"}, {nonew => $options->{nonew}, keep_all_props => $options->{keep_all_props}});

		# convert to json
		$json = PolyDB::Polymake_JSON_Conversion::json_save($obj, $metadata, $id, {projection=>$temp->{'db_template'}, modifier=>$temp->{'modifier'} });
	} else {
		
		# convert to json
		# save the given polymake object as is
		$json = PolyDB::Polymake_JSON_Conversion::json_save($obj, $metadata, $id);		
	}
		
	# insert into database
	my $o = $collection->insert_one(::JSON->new->utf8->decode($json));
	
	# return success
	return $o;
}


# @category Database Write Access
# Adds an object //obj// to the collection //col// in the database //db//.
# 
# Note that you need write access to the database for this.
# 
# Contained in extension ''polyDB''.
# @param Core::Object obj
# @param  String db             database name
# @param  String col            collection name
# @option String id             unique identifier
# @option String contributor    set the contributor
# @option Bool local            set to 1 if you want to use a local database (on localhost), default 0
# @option String username
# @option String password
# @option Bool use_template     set to 1 to match properties with a template object  
# @option Bool nonew            set to 1 if you don't want to compute missing properties but only delete surplus ones (only takes effect if use_template is also set to 1)
# @option Bool keep_all_props   keep all properties present in the polymake object (in the xml. not necessarily in the db representation)
# @option Core::Object template can be precomputed for better performance when applying to multiple objects (implies use_template => 1)
# @option MongoClient client
# @return String
user_function db_insert ($, $, $, { local => 0, id => "", contributor => undef, username => $db_user, password => $db_pwd, client => undef, use_template => 0, template => undef, template_key=> "default", nonew => 0, keep_all_props=> 0}) {
	my ($obj, $db, $col, $options) = @_;
 	 	
	# get client if not given by the options
	my $client;
	unless (defined($client = $options->{client})) {
		$client = get_client($options);
	}
	my $collection = get_collection($client, $db, $col);

	# the objects needs an id
	# FIXME currently no method implemented to generate one (could take the name)
	my $id;
	if ( !defined($options->{"id"}) ) {
		croak("no id defined, currently no method to generate one implemented");
	} else {
		$id = $options->{"id"};
	}
	
	# get template object if not given by the options
	my $temp;
	my $output;
	# check if use of template is wanted
	# check if template is given in the options, otherwise try to load one
	if ($options->{use_template} or defined($temp = $options->{template})) {
		unless (defined($temp)) {
			$temp = get_template($db, $col, local => $options->{local}, client => $client, template_key => $options->{template_key});
		}
		$output = insert_into_col($obj, $collection, $id, {template => $temp, template_key => $options->{template_key}, nonew => $options->{nonew}, keep_all_props => $options->{keep_all_props}, contributor => $options->{contributor} });

	} else {

		$output = insert_into_col($obj, $collection, $id, { nonew => $options->{nonew}, keep_all_props => $options->{keep_all_props}, contributor => $options->{contributor} });
	}
	
	unless (defined($options->{client})){
		undef $client;
	}
	
	if ( !$output->{acknowledged} ) {
		print join "-", keys %$output;
		croak("Error while inserting into database\n");
	}
}


# @category Database Write Access
# Adds an object from a file //file// to the collection //col// in the database //db//. First it matches its properties with the template object for this collection (unless your options say otherwise).
# 
# Note that you need write access to the database for this.
# 
# Contained in extension ''polyDB''.
# @param String file polymake object stored in a file
# @param String db database name
# @param String col collection name
# @option String id unique identifier, if you don't specify one, polyDB will try to create one from the object name or file name, if this does not work out one will be created automatically
# @option String contributor set the contributor
# @option Bool local set to 1 if you want to use a local database (on localhost), default 0
# @option String username
# @option String password
# @option Bool use_template set to 1 to match properties with a template object. 
# @option Bool nonew set to 1 if you don't want to compute missing properties but only delete surplus ones (only takes effect if use_template is also set to 1)
# @option Core::Object template can be precomputed for better performance when applying to multiple objects (implies use_template => 1)
# @option MongoClient client
# @return String
user_function db_insert_from_file($, $, $, {local => 0, username => $db_user, password => $db_pwd, id => "", contributor => undef, nonew => 0, use_template => 0, template => undef,  template_key=> "default", keep_all_props=> 0, client => undef}) {
	my ($file, $db, $col, $options) = @_;

	my $obj = User::load($file);
	
	my $id = $options->{id};
	
	unless ($id) {
		if ($id = generate_id($file, $db, $col)) {
			$options->{"id"} = $id;
		}
	}
	
	return db_insert($obj, $db, $col, $options);
}


# @category Database Write Access
# Adds a list of objects from a pdata file to the collection //col// in the database //db//. It assumes that the objects names can be used to generate unique ids. Objects with ids that are already contained in the database will be skipped.
# 
# TODO: It still processes (and inserts) the files one by one (which is fine if the objects are relatively large). If one wants to insert a large number of very small objects there is a more efficient way of doing this.
# 
# Note that you need write access to the database for this.
# 
# Contained in extension ''polyDB''.
# @param String file pdata file (created with [[save_data]])
# @param String db database name
# @param String col collection name
# @option String contributor
# @option Bool local set to 1 if you want to use a local database (on localhost), default 0
# @option String username
# @option String password
# @option Bool use_template set to 1 to match properties with a template object.
# @option Core::Object template (implies //use_templates//=>1)
# @option Bool nonew set to 1 if you don't want to compute missing properties but only delete surplus ones
# @option Bool verbose print database output
# @option MongoClient client
# FIXME old version
user_function db_insert_pdata ($, $, $, {contributor => undef, local => 0, username => $db_user, password => $db_pwd, nonew => 0, use_template => 0, move_dir => "", client => undef, rem_props => undef, template => undef, template_key => "default", verbose => 0}){
	my ($file, $db, $col, $options) = @_;
	
	# get client if not given by the options
	my $client;
	unless (defined($client = $options->{client})) {
		$client = get_client($options);
	}

	my $collection = get_collection($client, $db, $col);
	my $objects = User::load_data($file);

	my $use_tamplate = 0;
	my $temp;
	if ($options->{use_template} or defined($temp = $options->{template})) {
		unless (defined($temp)) {
			$temp = get_template($db, $col, local => $options->{local}, client => $client, template_key => $options->{template_key});
		}
		$use_tamplate = 1;
	}	
		
	my $count = 0;
	foreach my $obj (@$objects) {
		my $id = generate_id($obj->name, $db, $col);
		
		if ($collection->find_one({'_id' => $id})) {	
			if ($options->{verbose}) { print "id $id already exists\n"; }
			next;
		}
		
		my $output;
		if ( $use_tamplate ) {
			$output = insert_into_col($obj, $collection, $id, {template => $temp, nonew => $options->{nonew}, keep_all_props => $options->{keep_all_props}, contributor => $options->{contributor} });
		} else {
			$output = insert_into_col($obj, $collection, $id, { nonew => $options->{nonew}, keep_all_props => $options->{keep_all_props}, contributor => $options->{contributor} });
		}				
		if ( !$output->{acknowledged} ) {
			print join "-", keys %$output;
			croak("Error while inserting into database\n");
		}
		$count++;

	}
	
	unless (defined($options->{client})){
		undef $client;
	}
	print "Inserted $count objects.\n\n";
}



# @category Database Write Access
# Removes the object with a given //id// from the collection //col// in the database //db//.
# 
# Note that you need write access to the database for this.
# 
# Contained in extension ''polyDB''.
# @param String id
# @param String db database name
# @param String col collection name
# @option Bool local set to 1 if you want to use a local database (on localhost), default 0
# @option String username
# @option String password
# @option MongoClient client
# @return String
user_function db_remove ($, $, $, {local => 0, username => $db_user, password => $db_pwd, client => undef}) {
	my ($id, $db_name, $col_name, $options) = @_;

	# get client if not given by the options
	my $client;
	unless (defined($client = $options->{client})) {
		$client = get_client($options);
	}

	my $col = get_collection($client, $db_name, $col_name);
	
	return $col->remove({'_id' => $id});
}





################################################################################################################
################################################################################################################
################################################################################################################




# @category Database Write Access
# Adds a list of objects from files to the collection //col// in the database //db//. It assumes that the objects names can be used to generate unique ids. Objects with ids that are already contained in the database will be skipped (and moved to //move_dir// if indicated).
# 
# TODO: It still processes (and inserts) the files one by one (which is fine if the objects are relatively large). If one wants to insert a large number of very small objects there is a more efficient way of doing this.
# 
# Note that you need write access to the database for this.
# 
# Contained in extension ''polyDB''.
# @param Array<String> files list of file names
# @param String db database name
# @param String col collection name
# @option String contributor
# @option Bool local set to 1 if you want to use a local database (on localhost), default 0
# @option String username
# @option String password
# @option Bool use_template set to 1 to match properties with a template object. 
# @option Bool nonew set to 1 if you don't want to compute missing properties but only delete surplus ones
# @option String move_dir directory in which processed files are moved
# @option Bool verbose print database output
# @option MongoClient client
# FIXME old version
user_function db_batch_insert ($, $, $, {contributor => "", local => 0, username => $db_user, password => $db_pwd, nonew => 0, use_template => 0, move_dir => "", client => undef, rem_props => undef}){
	
	croak("this function is currently not available\n");
	
	my ($files, $db_name, $collection, $options) = @_;
	
	my @files = @$files;
	
	# get client if not given by the options
	my $client;
	unless (defined($client = $options->{client})) {
		$client = get_client($options);
	}

	# get template object if needed
	my $temp;
	if ($options->{use_template}){
		$temp = get_template($db_name, $collection, local => $options->{local}, client => $client);
	}

		
	print "inserting ...\n";
	foreach (@files) {
		my $obj = User::load($_);
		$obj->CONE_DIM;
		if ($options->{use_template} && !$options->{nonew}) {
			copy_properties($obj, $temp);
		}
	}

	my $col = get_collection($client, $db_name, $collection);
	
	my $md = $options->{move_dir};
	if ($md) {
		system("mkdir -p $md");
	}
	
	foreach my $file (@files) {
		my $obj = User::load($file);

	 	#check_type($obj, $db_name, $collection, $client);

		my $id = generate_id($obj->name, $db_name, $collection);
		
		if ($col->find_one({'_id'=>$id})) {
			if ($md) {
				system("mv $file $md");
			}	
			next;
		}

		my $json = pm2json($obj, {contributor => $options->{contributor}}, $rem_props, $id, 0, $temp);

		my $output = $col->insert($json);
		if ($options->{verbose}) {print "$output ";}

		if ($md) {
			system("mv $file $md");
		}
	}
	
	unless (defined($options->{client})){
		undef $client;
	}
	print "\n";
}




