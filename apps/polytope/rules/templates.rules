# Copyright (c) 2013 Silke Horn
# http://solros.de/polymake/poly_database
# 
# This file is part of the polymake extension poly_database.
# 
# poly_database is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# poly_database is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with poly_database.  If not, see <http://www.gnu.org/licenses/>.


# @category Database Insertion
# Sets the template object for the collection //col// of the database //db//.
# 
# Note that you need write access to the type database for this. You will be asked for credentials if you don't specify them.
# 
# Contained in extension ''poly_database''.
# @param Core::Object obj
# @param String db database name
# @param String col collection name
# @option Bool local set to 1 if you want to use a local database (on localhost), default 0
# @option String username
# @option String password
# @option Bool replace_type set to 1 if you want to replace type information for this collection
user_function set_template_object($, $, $, {local => 0, username => "", password => ""}) {
	my ($obj, $db, $col, $options) = @_;
	my $local = $options->{local};
	my $u = $options->{username};
	my $p = $options->{password};
	my $r = $options->{replace_type};

	unless ($u || $local) {
		my @cred = get_credentials;
		$u = $cred[0];
		$p = $cred[1];
	}

	my $client = get_client($local, $u, $p);

	my $t = get_type($client, $db, $col);


	my $app = $obj->type->application->name;
	my $objtype = $obj->type->full_name;

	unless ($r) {
		unless ($t->{'app'} eq $app) {
			croak("application $app of given object does not match type information for collection $col (requires object of type $t->{'app'}:$t->{'type'})");
		}
		unless ($t->{'type'} eq $objtype) {
			croak("type $obj of given object does not match type information for collection $col (requires object of type $t->{'app'}:$t->{'type'})");		
		}
	}
		
	my $tdb = $client->get_database($type_db);
	my $tcol = $tdb->get_collection("Types");

	my $output;
	if ($r) {
		$output = $tcol->update({'db'=>$db, 'col'=>$col}, {'$set' => {'app' => $app, 'type' => $objtype, 'template' => pm2json($obj,"","",1)} });
		
	} else {
		$output = $tcol->update({'db'=>$db, 'col'=>$col}, {'$set' => {'template' => pm2json($obj,"","",1)} });
	}

	if ($output->{ok}) {
		print "successfully set template object for collection $col in database $db\n"
	} else {
		print "an error occurred when trying to set the template object for collection $col in database $db\n";
	}
}


# @category Database Insertion
# Sets the template object for the collection //col// of the database //db//.
# 
# Contained in extension ''poly_database''.
# @param String db database name
# @param String col collection name
# @option Bool local set to 1 if you want to use a local database (on localhost), default 0
# @return Core::Object
user_function get_template_object($, $, {local => 0}) {
	my ($db, $col, $options) = @_;
	my $local = $options->{local};
	
	my $client = get_client($local);
		
	my $tcol = $client->get_database($type_db)->get_collection("Types");

	my $doc = $tcol->find({db => $db, col => $col})->next;
	
	my $r = doc2object($doc->{'template'}, $doc);
	
	return $r;
}





# @category Database Insertion
# Creates a new collection //col// in the database //db// for objects of type //app//://type//. You can optionally set the template object for this collection.
# 
# Note that you need write access to the type database for this. You will be asked for credentials if you don't specify them.
# 
# Contained in extension ''poly_database''.
# @param String db database name
# @param String col collection name
# @param String app application name for the objects
# @param String type type name for the objects
# @option Bool local set to 1 if you want to use a local database (on localhost), default 0
# @option String username
# @option String password
# @option Core::Object template
user_function set_type_info($, $, $, $, {local => 0, username => "", password => "", template => undef, version => ""}) {
	my ($db, $col, $app, $type, $options) = @_;
	my $local = $options->{local};
	my $u = $options->{username};
	my $p = $options->{password};

	unless ($u || $local) {
		my @cred = get_credentials;
		$u = $cred[0];
		$p = $cred[1];
	}

	my $client = get_client($local, $u, $p);
		
	my $tdb = $client->get_database($type_db);
	my $tcol = $tdb->get_collection("Types");

	my $output	= $tcol->save({'db'=>$db, 'col'=>$col, 'app'=>$app, 'type'=>$type});

	if ($output->{ok}) {
		print "successfully saved type information for collection $col in database $db\n"
	} else {
		print "an error occurred when trying to create collection $col in database $db\n";
	}
	
	if (defined($options->{template})) {
		set_template_object($options->{template}, $db, $col, local=>$options->{local}, username=>$u, password=>$p);
	}
}
